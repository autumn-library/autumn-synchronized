Функция ОбработатьЖелудь(Желудь, ОпределениеЖелудя) Экспорт

	Методы = ОпределениеЖелудя.НайтиМетодыСАннотациями("Синхронизировано");

	Если Методы.Количество() = 0 Тогда
		Возврат Желудь;
	КонецЕсли;

	ПостроительДекоратора = Новый ПостроительДекоратора(Желудь)
		.Поле(Новый Поле("НапильникСинхронизировано_Блокировка").ЗначениеПоУмолчанию(Новый БлокировкаРесурса()));

	ТелоПерехватчикаПеред =
	"
	|	НапильникСинхронизировано_Блокировка.Заблокировать();
	|	// Вижу немой вопрос в твоих глазах читатель, но это магический костыль, без этого
	|   // микро таймаута, почему-то не происходит должной синхронизации и весь смысл мониторов
	|	// теряется, т.е без задержки ФЗ получают старые значения свойств класса.
	|	// FIXME: Надо призвать @theEvilBeaver и попробовать разобраться с тем что тут происходит
	|	Приостановить(1);
	|";

	ТелоПерехватчикаПосле =
	"
	|	НапильникСинхронизировано_Блокировка.Разблокировать();
	|";

	Для каждого Метод Из Методы Цикл

		ПостроительДекоратора.Перехватчик(
			Новый Перехватчик(Метод.Имя)
				.ТипПерехватчика(ТипыПерехватчиковМетода.Перед)
				.Тело(ТелоПерехватчикаПеред)
		).Перехватчик(
			Новый Перехватчик(Метод.Имя)
				.ТипПерехватчика(ТипыПерехватчиковМетода.После)
				.Тело(ТелоПерехватчикаПосле)
		).Перехватчик(
			Новый Перехватчик(Метод.Имя)
				.ТипПерехватчика(ТипыПерехватчиковМетода.ПослеВызоваИсключения)
				.Тело(ТелоПерехватчикаПосле)
		);

	КонецЦикла;

	Возврат ПостроительДекоратора.Построить();

КонецФункции

&Напильник
Процедура ПриСозданииОбъекта()
КонецПроцедуры
