Функция ОбработатьЖелудь(Желудь, ОпределениеЖелудя) Экспорт

	Методы = ОпределениеЖелудя.НайтиМетодыСАннотациями("Синхронизировано");

	Если Методы.Количество() = 0 Тогда
		Возврат Желудь;
	КонецЕсли;

	ПостроительДекоратора = Новый ПостроительДекоратора(Желудь)
		.Поле(Новый Поле("НапильникСинхронизировано_Блокировка").ЗначениеПоУмолчанию(Новый БлокировкаРесурса()));

	ТелоПерехватчикаПеред = "НапильникСинхронизировано_Блокировка.Заблокировать();";
	ТелоПерехватчикаПосле = "НапильникСинхронизировано_Блокировка.Разблокировать();";

	Для каждого Метод Из Методы Цикл

		ПостроительДекоратора.Перехватчик(
			Новый Перехватчик(Метод.Имя)
				.ТипПерехватчика(ТипыПерехватчиковМетода.ПередСинхронизациейПолей)
				.Тело(ТелоПерехватчикаПеред)
		).Перехватчик(
			Новый Перехватчик(Метод.Имя)
				.ТипПерехватчика(ТипыПерехватчиковМетода.После)
				.Тело(ТелоПерехватчикаПосле)
		).Перехватчик(
			Новый Перехватчик(Метод.Имя)
				.ТипПерехватчика(ТипыПерехватчиковМетода.ПослеВызоваИсключения)
				.Тело(ТелоПерехватчикаПосле)
		);

	КонецЦикла;

	Возврат ПостроительДекоратора.Построить();

КонецФункции

&Напильник
Процедура ПриСозданииОбъекта()
КонецПроцедуры
